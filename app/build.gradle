apply plugin: 'com.android.application'
apply plugin: 'android-apt'
//apply plugin: 'me.ele.amigo'
apply plugin: 'com.antfortune.freeline'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.king.app.workhelper"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        freeline {
            hack true
        }

        //resConfigs "zh"//将语言资源限定为中文
    }

    //配置签名
    signingConfigs {
        config {
            keyAlias 'feng'
            keyPassword '123456'
            storeFile file('/key/WorkHelperAppKeyStore.jks')
            storePassword '123456'
        }
    }
    buildTypes {
        release {
            // 不显示Log, 在java代码中的调用方式为：BuildConfig.LOG_DEBUG
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true//代码混淆
            shrinkResources false//移除无用的资源. AS>=2.0时设为true编译出错，使用Inspect Code代替此功能。
            zipAlignEnabled true//Zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config//使用签名
            manifestPlaceholders = [app_label:"Test"]

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称示例为app-1.0-release-20170208.apk
                        def fileName = "app-${defaultConfig.versionName}-release-${releaseTime()}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            shrinkResources false
            manifestPlaceholders = [app_label:"Test"]
//            resValue "string", "app_label", "Test"//意为把名为app_label的string value值改为Test
        }
        preview {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            shrinkResources false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    // 'return void' removes the lint error: 'Not all execution paths return a value'.
    return void
}

dependencies {
    //公共基础库
    compile project(':applib')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //compile(name:'applib-0.0.1', ext:'aar')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:palette-v7:24.2.1'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:multidex:1.0.1'
    //使用ButterkNife-View注入框架
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    //EventBus
    compile 'com.squareup:otto:1.3.8'

    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    compile 'com.zhy:okhttputils:2.6.2'
    //compile 'pub.devrel:easypermissions:0.2.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    debugCompile 'com.antfortune.freeline:runtime:0.8.5'
    releaseCompile 'com.antfortune.freeline:runtime-no-op:0.8.5'
    testCompile 'com.antfortune.freeline:runtime-no-op:0.8.5'

    compile 'com.facebook.fresco:fresco:0.12.0'
    compile 'com.facebook.fresco:imagepipeline-okhttp:1.0.0'
    // 在 API < 14 上的机器支持 WebP 时，需要添加
    //compile 'com.facebook.fresco:animated-base-support:0.12.0'
    // 支持 GIF 动图，需要添加
    //compile 'com.facebook.fresco:animated-gif:0.12.0'
    // 支持 WebP （静态图+动图），需要添加
    //ompile 'com.facebook.fresco:animated-webp:0.12.0'
    //compile 'com.facebook.fresco:webpsupport:0.12.0'
    // 仅支持 WebP 静态图，需要添加
    //compile 'com.facebook.fresco:webpsupport:0.12.0'

    compile 'com.facebook.stetho:stetho:1.4.2'
}

checkstyle {
    toolVersion '6.1.1'
    showViolations true
}

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    configFile file("config/checkstyle.xml")
    include '**/*.java'
    exclude '**/gen/**'
    ignoreFailures true

    classpath = files()
}

repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
}

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}