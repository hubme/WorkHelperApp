apply plugin: 'com.android.application'
apply plugin: 'android-apt'
//apply plugin: 'me.ele.amigo'
apply plugin: 'com.antfortune.freeline'
apply plugin: 'checkstyle'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.king.app.workhelper"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        freeline {
            hack true
            productFlavor 'dev'
        }

        //resConfigs "zh"//将语言资源限定为中文

        //打包指定cpu架构的so库.
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86"
        }
    }

    //配置签名
    signingConfigs {
        config {
            keyAlias 'feng'
            keyPassword '123456'
            storeFile file('/key/WorkHelperAppKeyStore.jks')
            storePassword '123456'
        }
    }
    buildTypes {
        release {
            // 不显示Log, 在java代码中的调用方式为：BuildConfig.LOG_DEBUG
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true//代码混淆
            shrinkResources false//移除无用的资源. AS>=2.0时设为true编译出错，使用Inspect Code代替此功能。
            zipAlignEnabled true//Zipalign优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config//使用签名
            manifestPlaceholders = [app_label: "Test"]
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            shrinkResources false
            manifestPlaceholders = [app_label: "Test"]
//            applicationIdSuffix ".debug"
            signingConfig signingConfigs.config//使用签名
//            resValue "string", "app_label", "Test"//意为把名为app_label的string value值改为Test
        }
        //增加Build Type
        /*preview {
            signingConfig signingConfigs.config
        }*/
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //app-release-official-1.0.0-20170211.apk
                def fileName = "app-${variant.buildType.name}-${variant.productFlavors[0].name}-${variant.versionName}-${releaseTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    productFlavors {
        dev {
            manifestPlaceholders = [channel: "dev"]
        }
        /*official {
            manifestPlaceholders = [channel: "official"]
        }
        wandoujia {
            manifestPlaceholders = [channel: "wandoujia"]
        }
        //flavor名如果是数字开头，必须用引号引起来
        "360" {
            manifestPlaceholders = [channel: "360"]
        }*/
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'

        //过滤掉指定的so库
        exclude 'lib/arm64-v8a/libimagepipeline.so'
        exclude 'lib/x86/libweibosdkcore.so'
    }
    
    //需要去掉自定义的task.https://developer.android.com/studio/build/configure-apk-splits.html?hl=zh-cn#configure-abi-split
    splits {//拆分apk
        abi {//根据ABI拆分
            enable false//是否启用拆分
            reset()
            include 'armeabi', 'armeabi-v7a'//包含哪些ABI类型
            universalApk true//是否生成通用的APK
        }
    }

}

dependencies {
    //公共基础库
    compile project(':applib')
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //compile(name:'applib-0.0.1', ext:'aar')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:palette-v7:$rootProject.ext.supportLibVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibVersion"
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    compile 'com.android.support:multidex:1.0.1'
    //使用ButterkNife-View注入框架
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    //EventBus
    compile 'com.squareup:otto:1.3.8'

    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

    //一以下三个依赖版本要保持一致.否则:FATAL EXCEPTION
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    compile 'com.zhy:okhttputils:2.6.2'
    //compile 'pub.devrel:easypermissions:0.2.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    debugCompile 'com.github.markzhai:blockcanary-android:1.5.0'
    releaseCompile 'com.github.markzhai:blockcanary-no-op:1.5.0'

    debugCompile 'com.antfortune.freeline:runtime:0.8.7'
    releaseCompile 'com.antfortune.freeline:runtime-no-op:0.8.7'

    compile 'com.facebook.fresco:fresco:0.12.0'
    compile "com.facebook.fresco:imagepipeline-okhttp3:0.12.0"

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.facebook.stetho:stetho:1.5.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    
    compile 'com.google.android:flexbox:0.2.6'
    
    compile(name: 'weibosdk-1.0.0', ext: 'aar')
}

checkstyle {
    toolVersion '6.1.1'
    showViolations true
}

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    configFile file("config/checkstyle.xml")
    include '**/*.java'
    exclude '**/gen/**'
    ignoreFailures true

    classpath = files()
}

repositories {
    flatDir {
        dirs 'libs' //this way we can find the .aar file in libs folder
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}